# [Defining the Data Model in CDS Views](https://help.sap.com/docs/abap-cloud/abap-rap/defining-data-model-in-cds-views#travel-root-view-%252Fdmo%252Fi_travel_u)

## Travel Root View /DMO/I_Travel_U
The listing 1 (below) provides you with the implementation of the CDS data model for managing flights, where the database table /dmo/travel serves as the data source for the corresponding CDS view /DMO/I_Travel_U (note the camel case notation).

This CDS view defines the root entity of the data model and represents the root of the compositional hierarchy for the travel business object to be created.

From a structural point of view, a business object consists of a tree of nodes that are linked by special associations known as compositions. To define a composition relationship from the root to a child entity the keyword COMPOSITION is used. In our example, you specify the /DMO/I_Booking_U as child entity of the composition _Booking. As a result, the booking node is defined as a direct sub node to the business object’s root. With cardinality [0 .. *] you express that any number of booking instances can be assigned to each travel instance.

To be able to access business data from semantically related entities, a set of associations is defined in the CDS source code. These associations refer to CDS views that are part of our demo application scenario. Some of these views are used primarily as text views for retrieving text information and as value help provider views for specific UI fields, see Projecting the Data Model in CDS Projection Views.

Except for the administrative fields createdby, lastchangedby, and createdat, all fields of the data source table /dmo/travel have been added to the element list in the CDS view. The database table provides several administrative fields that are used for administrative data which usually includes the user who created or last changed an instance and the corresponding timestamps. In this example however, the element LastChangedAt plays a special part, as it is used for ETag checks to determine whether two representations of an entity are the same. If the representation of the entity ever changes, a new and different ETag value is assigned. ETags play a significant part in the lock lifetime when working with business objects.

For all elements, we can provide an alias to provide a readable name for the elements.

The price elements and the element CurrencyCode have a semantic relationship. This relationship is manifested in the business object view via the @semantics annotations. In a Fiori UI, the amount and the currency value can then be displayed together.

Expand the following code sample to view the source code of CDS Root View /DMO/I_Travel_U.

Sample Code
```
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Travel view - CDS data model'

define root view entity /DMO/I_Travel_U
  as select from /dmo/travel as Travel -- the travel table is the data source for this view

  composition [0..*] of /DMO/I_Booking_U as _Booking

  association [0..1] to /DMO/I_Agency    as _Agency   on $projection.AgencyID = _Agency.AgencyID
  association [0..1] to /DMO/I_Customer  as _Customer on $projection.CustomerID = _Customer.CustomerID
  association [0..1] to I_Currency       as _Currency on $projection.CurrencyCode = _Currency.Currency
  association [1..1] to /DMO/I_Travel_Status_VH as _TravelStatus on $projection.Status = _TravelStatus.TravelStatus
{
  key Travel.travel_id     as TravelID,
      Travel.agency_id     as AgencyID,
      Travel.customer_id   as CustomerID,
      Travel.begin_date    as BeginDate,
      Travel.end_date      as EndDate,
      @Semantics.amount.currencyCode: 'CurrencyCode'
      Travel.booking_fee   as BookingFee,
      @Semantics.amount.currencyCode: 'CurrencyCode'
      Travel.total_price   as TotalPrice,
      Travel.currency_code as CurrencyCode,
      Travel.description   as Memo,
      Travel.status        as Status,
      Travel.lastchangedat as LastChangedAt,

      /* Associations */
      _Booking,
      _Agency,
      _Customer,
      _Currency,
      _TravelStatus
}
```

## Booking View /DMO/I_Booking_U
Listing 2 (below) provides you with a data model implementation of the booking entity. In the data definition of the root entity /DMO/I_Travel_U, you specified the booking entity /DMO/I_Booking_U as a child entity. This composition relationship requires an association to their compositional parent entity for the booking child entity to be specified in the data model implementation. This relationship is expressed by the keyword ASSOCIATION TO PARENT. Using this syntax, you define the CDS entity /DMO/I_Booking_U as a direct sub node in the compositional hierarchy of the travel business object structure.

The SELECT list includes all elements of a booking entity that are relevant for building the BO structure. These elements are provided with an alias. Like in the Travel CDS view, the semantic relationship between Price and CurrencyCode is established with the @Semantics annotation.

To be able to access data from other entities, a set of additional associations (_Customer, _Carrier, and _Connection) is defined in the CDS source code. These views are primarily used as text views for retrieving text information and as value help provider views for specific booking fields on the UI, Projecting the Data Model in CDS Projection Views.

Expand the following code sample to view the source code of CDS view /DMO/I_Booking_U.


Sample Code
```
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Booking view'

define view entity /DMO/I_Booking_U
  as select from /dmo/booking as Booking

  association        to parent /DMO/I_Travel_U     as _Travel     on  $projection.TravelID = _Travel.TravelID

  association [1..1] to /DMO/I_Customer            as _Customer   on  $projection.CustomerID = _Customer.CustomerID
  association [1..1] to /DMO/I_Carrier             as _Carrier    on  $projection.AirlineID = _Carrier.AirlineID
  association [1..1] to /DMO/I_Connection          as _Connection on  $projection.AirlineID    = _Connection.AirlineID
                                                                  and $projection.ConnectionID = _Connection.ConnectionID
{

  key Booking.travel_id     as TravelID,
  key Booking.booking_id    as BookingID,
      Booking.booking_date  as BookingDate,
      Booking.customer_id   as CustomerID,
      Booking.carrier_id    as AirlineID,
      Booking.connection_id as ConnectionID,
      Booking.flight_date   as FlightDate,
      @Semantics.amount.currencyCode: 'CurrencyCode'
      Booking.flight_price  as FlightPrice,
      Booking.currency_code as CurrencyCode,

      /* Associations */
      _Travel,
      _Customer,
      _Carrier,
      _Connection
}
```

## Travel Status View /DMO/I_Travel_Status_VH
Listing 3 (below) provides you with a data definition for handling travel status data. The CDS view /DMO/I_Travel_Status_VH serves as a value help view providing the possible travel status values.

The database table /dmo/trvl_stat is the data source for the CDS view. The travel status field in table /dmo/trvl_stat has been added to the element list in the CDS view.

To enable a value selection via a drop-down-menu, the annotation @ObjectModel.resultSet.sizeCategory: #XS is used.

The associated CDS view /DMO/I_Travel_Status_VH_Text serves as a provider view for localized texts. Hence, the CDS view /DMO/I_Travel_Status_VH contains an association as well as the annotation @ObjectModel.text.association: '_Text' pointing to the CDS view /DMO/I_Travel_Status_VH_Text.

To ensure that only the texts provided by the associated CDS view /DMO/I_Travel_Status_VH_Text are displayed in the value help, the annotation @UI.textArrangement: #TEXT_ONLY is used.

The annotation @UI.lineItem: [{importance: #HIGH}] ensures that the travel status data are displayed even when little display space is available.

The @Objectmodel.usageType annotations define the expected performance parameters and contained data class for the value help view. For more information, refer to ObjectModel Annotations.

Expand the following code sample to view the source code of the travel status view /DMO/I_Travel_Status_VH.


Sample Code
```
@AbapCatalog.viewEnhancementCategory: [#NONE]
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Travel Status Value Help'
@Metadata.ignorePropagatedAnnotations: true
@ObjectModel.usageType:{
 serviceQuality: #A,
 sizeCategory: #S,
 dataClass: #MASTER
 }
@ObjectModel.resultSet.sizeCategory: #XS
define view entity /DMO/I_Travel_Status_VH
  as select from /dmo/trvl_stat

  association [0..*] to /DMO/I_Travel_Status_VH_Text as _Text on $projection.TravelStatus = _Text.TravelStatus

{
      @UI.textArrangement: #TEXT_ONLY
      @UI.lineItem: [{importance: #HIGH}]
      @ObjectModel.text.association: '_Text'
  key travel_status as TravelStatus,

      _Text
}
```

## Travel Status Text View /DMO/I_Travel_Status_VH_Text
Listing 4 (below) provides you with a data definition for handling travel status text data. This CDS entity provides the texts for the travel statuses on the list report page and in the value help.

The database table /dmo/trvl_stat_t is the data source for the corresponding CDS view /DMO/I_Travel_Status_VH_Text. All fields have been added to the element list in the CDS view.

To enable a value selection via a drop-down-menu, the annotation @ObjectModel.resultSet.sizeCategory: #XS is used.

The annotation @ObjectModel.text.element: ['Text'] establishes the link between the annotated element and its descriptive element Text. By using the annotation @Semantics.text: true for the element Text, at runtime, this field is read from the database and filtered by the logon language of the OData consumer automatically. The annotation @Semantics.language: true specifies the field used for this language filter.

The @Objectmodel.usageType annotations define the expected performance parameters and contained data class for the value help view. For more information, refer to ObjectModel Annotations.

Expand the following code sample to view the source code of travel status text view /DMO/I_Travel_Status_VH_Text.

Sample Code
```
@AbapCatalog.viewEnhancementCategory: [#NONE]
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Travel Status Value Help'
@Metadata.ignorePropagatedAnnotations: true
@ObjectModel.usageType:{
 serviceQuality: #A,
 sizeCategory: #S,
 dataClass: #MASTER
 }
@ObjectModel.resultSet.sizeCategory: #XS
define view entity /DMO/I_Travel_Status_VH_Text
  as select from /dmo/trvl_stat_t

  association [1..1] to /DMO/I_Travel_Status_VH as _TravelStatus on $projection.TravelStatus = _TravelStatus.TravelStatus

{
      @ObjectModel.text.element: ['Text']
      key travel_status as TravelStatus,

      @Semantics.language: true
      key language      as Language,

      @Semantics.text: true
      text          as Text,

      _TravelStatus
}
```

## Travel Agency View /DMO/I_Agency 
Listing 5 (below) provides you with a data definition for handling travel agency data. The database table /dmo/agency is the data source for the corresponding CDS view /DMO/I_Agency.

Since the travel agency's data can vary from one country to another, the data model refers to the I_Country view using the association _Country.

This CDS entity also serves as a text provider view. For this purpose, the annotation @Semantics.text: true is used to identify the Name element as a text element, which - in this case - points to a textual description of agency names. In the travel and the booking projection views, the associated text element is added as a field to the referencing entity. At runtime, this field is read from the database and filtered by the logon language of the OData consumer automatically.

In addition, the data model enables search capabilities on the Name element: The annotation @Search.searchable: true marks the CDS view as searchable, whereas @Search.defaultSearchElement: true specifies that the annotated Name element is to be considered in a full-text search. For detailed information, see: Enabling Text and Fuzzy Searches in SAP Fiori Apps

Expand the following code sample to view the source code of agency CDS view /DMO/I_Agency.


Sample Code

```
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Agency view - CDS data model'
@Search.searchable: true

define view entity /DMO/I_Agency

  as select from /dmo/agency as Agency -- the agency table serves as the data source for this view

  association [0..1] to I_Country as _Country on $projection.CountryCode = _Country.Country

{   

    key Agency.agency_id        as AgencyID,    
    @Semantics.text: true
    @Search.defaultSearchElement: true
    @Search.fuzzinessThreshold: 0.8
    Agency.name                 as Name,
    Agency.street               as Street,
    Agency.postal_code          as PostalCode,
    Agency.city                 as City,
    Agency.country_code         as CountryCode,
    Agency.phone_number         as PhoneNumber,
    Agency.email_address        as EMailAddress,
    Agency.web_address          as WebAddress,

    /* Associations */
    _Country
}
					
```

## Customer View /DMO/I_Customer 
Listing 6 (below) is used as a data model implementation for managing passenger data. The database table /dmo/customer serves as the data source for the corresponding CDS view /DMO/I_Customer.

Except for the administrative fields (createdby, createdat, lastchangedat and lastchangedby), all fields of the table /dmo/customer have been added to the element list in the CDS view.

Since a passenger’s data can vary from one country to another, the data model refers to the I_Country view using a corresponding association _Country.

The annotation @Semantics.text: true is added to the LastName element. This element serves as a text element, which - in this case - points to texts with customer names. This text annotation allows you to use this customer view as a text provider for the associated elements in the target views /DMO/I_Travel_U and /DMO/I_Booking_U.

Expand the following code sample to view the source code of CDS view /DMO/I_Customer.

Sample Code

```
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Customer view - CDS data model'
@Search.searchable: true

define view entity /DMO/I_Customer
  as select from /dmo/customer as Customer -- the customer table serves as the data source

  association [0..1] to I_Country as _Country on $projection.CountryCode = _Country.Country
{
    key Customer.customer_id    as CustomerID, 
    Customer.first_name         as FirstName,
    @Semantics.text: true
    @Search.defaultSearchElement: true
    @Search.fuzzinessThreshold: 0.8  
    Customer.last_name          as LastName, 
    Customer.title              as Title, 
    Customer.street             as Street, 
    Customer.postal_code        as PostalCode, 
    Customer.city               as City, 
    Customer.country_code       as CountryCode, 
    Customer.phone_number       as PhoneNumber, 
    Customer.email_address      as EMailAddress,

    /* Associations */
    _Country

}
```

## Flight View /DMO/I_Flight
Listing 7 (below) provides you with a data definition for flights. The data of specific flights is stored in the database table /dmo/flight, which serves as the data source for the corresponding CDS view /DMO/I_Flight.

As demonstrated in listing 2 (above), you can implement the value help with additional binding. To define filter conditions for the value help based on the same value help provider, the flight view /DMO/I_Flight is used to filter the value help result list for the annotated elements.

Expand the following code sample to view the source code of CDS view /DMO/I_Flight.

Sample Code

```
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Flight view'

define view entity /DMO/I_Flight as select from /dmo/flight as Flight
{ 
 
    key Flight.carrier_id          as AirlineID, 
    key Flight.connection_id       as ConnectionID, 
    key Flight.flight_date         as FlightDate,
    @Semantics.amount.currencyCode: 'CurrencyCode'  
    Flight.price                   as Price, 
    Flight.currency_code           as CurrencyCode, 
    Flight.plane_type_id           as PlaneType, 
    Flight.seats_max               as MaximumSeats, 
    Flight.seats_occupied          as OccupiedSeats
}
```

## Flight Connections View /DMO/I_Connection
Listing 8 (below) is used as a data definition for flight connections. The flight connections are stored in the database table /dmo/connection, which serves as the data source for the corresponding CDS view /DMO/I_Connection.

Except for the administrative fields, all fields in the table /dmo/connection have been added to the element list in the CDS view.

Using the annotation @Semantics.quantity.unitOfMeasure, the DistanceUnit element is referenced as an element containing a unit of measure for the value stored in the element Distance.

Expand the following code sample to view the source code of CDS view /DMO/I_Connection.

Sample Code

```
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Connection view'

define view entity /DMO/I_Connection
  as select from /dmo/connection as Connection
{

  key Connection.carrier_id              as AirlineID,
  key Connection.connection_id           as ConnectionID,

      Connection.airport_from_id         as DepartureAirport,
      Connection.airport_to_id           as DestinationAirport,
      Connection.departure_time          as DepartureTime,
      Connection.arrival_time            as ArrivalTime,
      @Semantics.quantity.unitOfMeasure: 'DistanceUnit'
      Connection.distance                as Distance,
      Connection.distance_unit           as DistanceUnit
}
```

## Carrier View /DMO/I_Carrier
The following data definition for the carrier CDS entity provides you with IDs and names of airlines that are stored in the database table /DMO/CARRIER.

This CDS entity mainly serves as a text provider view. It provides text data through text associations as defined in the travel and the booking views (Listing 1 and Listing 2).

For this purpose, a text annotation is required at element level in order to annotate the text elements from the view’s element list: In this example, the Name element is identified as the text element.

Expand the following code sample to view the source code of CDS view /DMO/I_Carrier.


Sample Code

```
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Carrier view'

define view entity /DMO/I_Carrier as select from /dmo/carrier as Airline 
{ 
  key Airline.carrier_id        as AirlineID, 
  
  @Semantics.text: true
  Airline.name                  as Name, 
  
  Airline.currency_code         as CurrencyCode    
}
```